buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def gitSha = 'git describe --always --dirty --match none'.execute().text.trim()
def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.meiste.tempalarm"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 230
        versionName "2.3.0-" + gitSha

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    lintOptions {
        textReport true
        xmlReport false
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix "-dev"
        }
    }
}

ext {
    supportLibVersion = '28.0.0'
    googleFirebaseVersion = '11.4.2'
    butterKnifeVersion = '9.0.0'
    leakCanaryVersion = '1.6.3'
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$supportLibVersion"
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:support-media-compat:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.google.android.gms:play-services-base:$googleFirebaseVersion"
    implementation "com.google.firebase:firebase-database:$googleFirebaseVersion"
    implementation "com.google.firebase:firebase-messaging:$googleFirebaseVersion"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    implementation 'com.jjoe64:graphview:3.1.4'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    testImplementation 'junit:junit:4.12'
}

// get signing config from gradle.properties file
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

apply plugin: 'com.google.gms.google-services'
