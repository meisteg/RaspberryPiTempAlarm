buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "com.meiste.tempalarm"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 110
        versionName "1.1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    lintOptions {
        textReport true
        xmlReport false
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            versionNameSuffix "-dev"
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'com.jakewharton.timber:timber:2.5.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jjoe64:graphview:3.1.4'
    compile 'com.meiste.greg:gcm:1.0'
    compile project(path: ':backend', configuration: 'android-endpoints')
    compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
        transitive = true;
    }
}

// get signing config from gradle.properties file
if (project.hasProperty('storePassword')
        && project.hasProperty('keyAlias')
        && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}
